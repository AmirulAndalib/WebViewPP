plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp' version '1.7.10-1.0.6'
}

android {
    signingConfigs {
        release {
            storePassword ANDROID_STORE_PASSWORD
            keyPassword ANDROID_KEY_PASSWORD
            keyAlias ANDROID_KEY_ALIAS
            storeFile file(ANDROID_STORE_FILE)
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    compileSdk 33

    defaultConfig {
        applicationId "cn.wankkoree.xp.webviewpp"
        minSdk 27
        targetSdk 33
        versionCode 15
        versionName 'v3.0.0'
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources  true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        dev {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources  false
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources  false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = 11
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'cn.wankkoree.xp.webviewpp'

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${defaultConfig.applicationId}-${defaultConfig.versionName}_${defaultConfig.versionCode}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    // Xposed Hook
    compileOnly 'de.robv.android.xposed:api:82'                                 // Xposed
    implementation 'com.highcapable.yukihookapi:api:1.1.4'                      // Yuki Hook API
    ksp 'com.highcapable.yukihookapi:ksp-xposed:1.1.4'                          // Yuki Hook API - KSP

    // 语言特性支持
    implementation 'androidx.core:core-ktx:1.9.0'                               // Jetpack
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'             // Jetpack - Lifecycle
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'     // Kotlin - Coroutines Android

    // 界面支持
    implementation 'androidx.appcompat:appcompat:1.5.1'                         // Jetpack - AppCompat
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'           // Jetpack - Constraint Layout
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'       // Jetpack - Swipe Refresh Layout
    implementation 'androidx.gridlayout:gridlayout:1.0.0'                       // Jetpack - Grid Layout
    implementation 'com.google.android.material:material:1.6.1'                 // Material Design
    implementation 'com.google.android.flexbox:flexbox:3.0.0'                   // Flexbox
    implementation 'io.noties.markwon:core:4.6.2'                               // Markdown

    // 功能支持
    implementation 'androidx.palette:palette-ktx:1.0.0'                         // Jetpack - Palette
    implementation 'com.github.kittinunf.fuel:fuel:2.3.1'                       // Fuel
    implementation 'com.github.kittinunf.fuel:fuel-android:2.3.1'               // Fuel - Android
    implementation 'com.github.kittinunf.fuel:fuel-gson:2.3.1'                  // Fuel - Gson
    implementation 'com.google.code.gson:gson:2.9.0'                            // Gson
}